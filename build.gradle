buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "1.9.1"
if (System.getenv().BUILD_NUMBER != null) version += "." + System.getenv().BUILD_NUMBER

targetCompatibility = 1.6
sourceCompatibility = 1.6

group= "net.dries007"
archivesBaseName = "HoloInventory"

minecraft {
    version = "1.7.10-10.13.1.1224"
    runDir = "jars"
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
} 

file('version.' + project.minecraft.version + '.txt').write version

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task sourcesJar(type: Jar) {
    from "LICENSE.txt"
    from sourceSets.main.allSource
    classifier = 'src'
    appendix = project.minecraft.version
}

task deobfJar(type: Jar) {
    from "LICENSE.txt"
    from sourceSets.main.output
	classifier = 'dev'
    appendix = project.minecraft.version
}

jar {
    from "LICENSE.txt"
	exclude("com/**")
    appendix = project.minecraft.version
}

artifacts {
    archives sourcesJar
    archives deobfJar
}
